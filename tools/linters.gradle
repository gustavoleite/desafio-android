apply plugin: 'checkstyle'
apply from: "$rootDir/tools/linters/detekt/detekt.gradle"

checkstyle {
    toolVersion = "8.7"
}

task checkstyle(type: Checkstyle) {
    group "Verification"
    configFile file("${rootDir}/tools/linters/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec) {
    group "Verification"
    description "Check kotlin code style."
    classpath configurations.ktlint
    main 'com.pinterest.ktlint.Main'
    args "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml", "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    group "Verification"
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    ktlint "com.pinterest:ktlint:0.37.2"
}
